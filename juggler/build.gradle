apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

def groupId = "me.ilich"
def artifactId = "juggler"
def version = "0.3.0"

def releaseDir = "${buildDir}/release"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

configurations {
    javadocDependencies
}

task javaDocs(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependencies
}

task javaDocsJar(type: Jar, dependsOn: javaDocs) {
    classifier = 'javadoc'
    from javaDocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        repository(url: "file://${releaseDir}")
    }
}

task generateBintrayJson {
    def template = new File("bintray_template.json").text
    File configFileText = new File("bintray.json")
    configFileText.createNewFile()
    configFileText.write(template.replace('[version]', version))
}

artifacts {
    archives sourcesJar
    archives javaDocsJar
}

dependencies {
    ext {
        supportLibVersion = '23.4.0'
        espressoVersion = '2.2.2'
    }
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    testCompile 'junit:junit:4.12'

    javadocDependencies "com.android.support:appcompat-v7:$supportLibVersion"
    javadocDependencies "com.android.support:support-annotations:$supportLibVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile project(':juggler-annotations')

}
repositories {
    mavenCentral()
}