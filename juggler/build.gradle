apply plugin: 'com.android.library'
apply plugin: 'maven'

def groupId = "me.ilich"
def artifactId = "juggler"
def version = "0.1.22"

def releaseDir = "${buildDir}/release"

android {
    lintOptions {
        disable 'RestrictedApi'
    }
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    javadocDependencies
}

task javaDocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependencies
}

task javaDocsJar(type: Jar, dependsOn: javaDocs) {
    classifier = 'javadoc'
    from javaDocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        repository(url: "file://${releaseDir}")
    }
}

task generateBintrayJson {
    def template = new File("bintray_template.json").text
    File configFileText = new File("bintray.json")
    configFileText.createNewFile()
    configFileText.write(template.replace('[version]', version))
}

artifacts {
    archives sourcesJar
    archives javaDocsJar
}

dependencies {
    ext {
        supportLibVersion = '28.0.0'
        espressoVersion = '2.2.2'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    api 'junit:junit:4.12'

    javadocDependencies "com.android.support:appcompat-v7:$supportLibVersion"
    javadocDependencies "com.android.support:support-annotations:$supportLibVersion"

}